apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: FilamentToolsPlugin

project.ext.isSample = true

filamentTools {
    cmgenArgs = "-q --format=ktx --size=256 --extract-blur=0.1 --deploy=src/main/assets/envs/venetian_crossroads_2k"
    iblInputFile = file("../../../third_party/environments/venetian_crossroads_2k.hdr")
    iblOutputDir = file("src/main/assets/envs")
}

task copyMesh(type: Copy) {
    from "../../../third_party/models/BusterDrone"
    into "src/main/assets/models"
}

preBuild.dependsOn copyMesh

clean.doFirst {
    delete "src/main/assets"
}

android {
    compileSdkVersion versions.compileSdk

    // The following lines enable validation layers with the Vulkan backend by copying
    // the appropriate files from the NDK to the device. To trap validation errors, be
    // sure to also use a debug build of Filament.
    if (project.hasProperty("filament_supports_vulkan")) {
        sourceSets.main.jniLibs {
            srcDirs = ["${android.ndkDirectory}/sources/third_party/vulkan/src/build-android/jniLibs"]
        }
    }

    defaultConfig {
        applicationId "com.google.android.filament.gltf"
        minSdkVersion 19
        targetSdkVersion versions.targetSdk
        missingDimensionStrategy 'functionality', 'full'
    }
}

dependencies {
    implementation deps.kotlin
    implementation project(':filament-android')
    implementation project(':gltfio-android')
    implementation project(':filament-utils-android')
}
